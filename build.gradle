plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.7'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'jacoco'
	id 'checkstyle'
	id 'org.openapi.generator' version '5.1.0'
}

group = 'com.scotiabank'
version = '1.0.0-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_11
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'io.r2dbc:r2dbc-h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

jacoco {
	toolVersion = "0.8.9"
	reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

test {
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it,
					exclude: ['**/model/**', '**/config/*Properties.*', '**/config/*Configuration.*'])
		}))
	}
}

checkstyle {
	toolVersion = "10.3.3"
	configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
	configProperties = [
			'checkstyle.suppressions.file': file("${project.rootDir}/config/checkstyle/suppressions.xml")
	]
}

openApiGenerate {
	generatorName = "java"
	inputSpec = "$rootDir/src/main/resources/swagger.yaml".toString()
	outputDir = "$layout.buildDirectory/generated".toString()
	apiPackage = "com.bcp.atlas.services.api"
	modelPackage = "com.bcp.atlas.services.model"
	invokerPackage = "com.bcp.atlas.services.invoker"
	configOptions = [
			dateLibrary: "java8"
	]
}

